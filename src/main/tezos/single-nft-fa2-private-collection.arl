/**
* Private : only owner or minter can mint
* Single  : ERC721 style
**/
archetype single_nft_private(owner : address)
with metadata ""

//----------------------------------------------------------------------------
// Constants
//----------------------------------------------------------------------------

constant CALLER_NOT_OWNER         : string = "CALLER_NOT_OWNER"
constant CONTRACT_NOT_PAUSED      : string = "CONTRACT_NOT_PAUSED"
constant CONTRACT_PAUSED          : string = "CONTRACT_PAUSED"
constant EXPIRY_TOO_BIG           : string = "EXPIRY_TOO_BIG"
constant FA2_INSUFFICIENT_BALANCE : string = "FA2_INSUFFICIENT_BALANCE"
constant FA2_INVALID_AMOUNT       : string = "FA2_INVALID_AMOUNT"
constant FA2_NOT_OPERATOR         : string = "FA2_NOT_OPERATOR"
constant INVALID_CALLER           : string = "INVALID_CALLER"
constant MISSIGNED                : string = "MISSIGNED"
constant MISSING_OWNER_CANDIDATE  : string = "MISSING_OWNER_CANDIDATE"
constant NO_ENTRY_FOR_USER        : string = "NO_ENTRY_FOR_USER"
constant NOT_FOUND                : string = "NOT_FOUND"
constant PERMIT_EXPIRED           : string = "PERMIT_EXPIRED"
constant PERMIT_NOT_FOUND         : string = "PERMIT_NOT_FOUND"
constant PERMIT_USER_NOT_FOUND    : string = "PERMIT_USER_NOT_FOUND"
constant SIGNER_NOT_FROM          : string = "SIGNER_NOT_FROM"
constant TOKEN_NOT_FOUND          : string = "FA2_TOKEN_UNDEFINED"

//----------------------------------------------------------------------------
// Owner role transfer
//----------------------------------------------------------------------------

variable owner_candidate : option<address> = none

/**
* Declares candidate for ownership
* @param candidate address
*/
entry declare_ownership(candidate : address) {
  called by owner
  effect {
    owner_candidate := some(candidate);
  }
}

/**
* Claims ownership of the contract
* Note: fails if caller is not declared candidate
*/
entry claim_ownership() {
  constant {
    owner_candidate_addr ?is owner_candidate otherwise MISSING_OWNER_CANDIDATE;
  }
  require {
    co1: owner_candidate_addr = caller otherwise INVALID_CALLER
  }
  effect {
    owner := owner_candidate_addr;
    owner_candidate := none
  }
}

//----------------------------------------------------------------------------
// Contract execution pause/resume
//----------------------------------------------------------------------------

//----------------------------------------------------------------------------
// Contract execution pause/resume
//----------------------------------------------------------------------------

variable paused : bool = false

/**
 * Pause the contract execution
 */
entry pause() {
  called by owner
  require {
    pausable_r1: is_not_paused()
  }
  effect {
    paused := true
  }
}

/**
 * Resume the contract execution
 */
entry unpause() {
  called by owner
  require {
    pausable_r2: paused otherwise CONTRACT_NOT_PAUSED
  }
  effect {
    paused := false
  }
}

/**
 * Asserts that the contract execution is not paused
 */
function is_not_paused() : bool {
  do_fail_if(paused, CONTRACT_PAUSED);
  return true
}

//----------------------------------------------------------------------------
// Metadata
//----------------------------------------------------------------------------

entry set_metadata(ikey: string, idata : bytes) {
  called by owner
  require { r3 : is_not_paused() }
  effect {
    metadata := put(metadata, ikey, idata)
  }
}

//----------------------------------------------------------------------------
// Minter role
//----------------------------------------------------------------------------

asset minters identified by minter {
  minter: address
}

entry add_minter(aminter: address) {
  called by owner
  require { r9: is_not_paused() }
  effect {
    minters.add_update(aminter, {})
  }
}

entry remove_minter(rminter: address) {
  called by owner
  require { r10: is_not_paused() }
  effect {
    minters.remove(rminter)
  }
}

//----------------------------------------------------------------------------
// Data types
//----------------------------------------------------------------------------

record part {
  partAccount : address;
  partValue   : nat;
}

asset royalties identified by rtokenid to big_map {
  rtokenid : nat;
  rvalue : list<part>;
}

asset ledger identified by ltoken to big_map {
  ltoken     : nat;
  lowner     : address;
}

asset operators identified by oaddr otoken oowner to big_map {
  oaddr       : address;
  otoken      : nat;
  oowner      : address;
  ounit       : unit = Unit;
}

asset token_metadata to big_map {
  ftoken_metadata : nat;
  token_id        : nat;
  token_info      : map<string, bytes>;
}

record operator_param {
  opp_owner    : address;
  opp_operator : address;
  opp_token_id : nat
} as ((owner, (operator, token_id)))

record transfer_destination {
  to_dest           : address;
  token_id_dest     : nat;
  token_amount_dest : nat
} as ((%to, (token_id, amount)))

record balance_of_request {
  bo_owner : address;
  btoken_id : nat;
} as ((owner, token_id))

record balance_of_response {
  request : balance_of_request;
  balance_ : nat;
} as ((request, balance))

record user_permit {
  expiry : option<nat>;
  created_at : date;
}

asset permits to big_map {
  user_address : address;
  counter      : nat = 0;
  user_expiry  : option<nat> = none;
  user_permits : map<bytes, user_permit> = [];
}

asset operators_for_all identified by fa_oaddr fa_oowner to big_map {
  fa_oaddr    : address;
  fa_oowner   : address;
  fa_empty    : unit = Unit;
}

variable default_expiry : nat =  31556952

//----------------------------------------------------------------------------
// Operators
//----------------------------------------------------------------------------

/**
* Adds or removes operators for specified tokens
* @param upl list of add or remove operator specification
*/
entry update_operators (upl : list<or<operator_param, operator_param>>) {
  require { r0 : is_not_paused() }
  effect {
    for up in upl do
      match up with
      | left(param)  -> (* add *)
        do_require(param.opp_owner = caller , CALLER_NOT_OWNER);
        operators.add_update((param.opp_operator, param.opp_token_id, param.opp_owner), {})
      | right(param) -> (* remove *)
        do_require(param.opp_owner = caller , CALLER_NOT_OWNER);
        operators.remove((param.opp_operator, param.opp_token_id, param.opp_owner))
      end;
    done;
  }
}

/**
* Adds or removes an operator for *all* transfers for caller's tokens
* @param upl list of add or remove operator address
*/
entry update_operators_for_all (upl : list<or<address, address>>) {
  require { r1 : is_not_paused() }
  effect {
    for up in upl do
      match up with
      | left(op)  -> (* add *)
        operators_for_all.add_update((op, caller), {})
      | right(op) -> (* remove *)
        operators_for_all.remove((op, caller))
      end;
    done;
  }
}

function check_owner_and_operator(txs : list<address * list<transfer_destination>>) : bool {
  var res = true;
  for tx in txs do
    const %from = tx[0];
    const tds = tx[1];
    for td in tds do begin
      res &=
        if caller <> %from then
          (operators.contains((caller,td.token_id_dest,%from)) or
           operators_for_all.contains((caller, %from)))
        else ledger[td.token_id_dest].lowner = caller;
    end
    done
  done;
  return res
}

//----------------------------------------------------------------------------
// Feeless Expiration
//----------------------------------------------------------------------------

function get_default_expiry(addr : address) : nat {
  var res = default_expiry;
  if permits.contains(addr) then
  match permits[addr].user_expiry with
  | some v -> res := v
  | none -> res := default_expiry
  end;
  return res
}

function get_expiry(addr : address, permit_key : bytes) : nat {
  return (permits[addr] ? let d = (the.user_expiry ? the : default_expiry) in
    (the.user_permits[permit_key] ? (the.expiry ? the : default_expiry) : d) : default_expiry
  )
}

function has_expired(iuser_permit : user_permit, iexpiry: nat) : bool {
  match iuser_permit.expiry with
  | some(v) -> return (iuser_permit.created_at + (v * 1s) < now)
  | none -> return (iuser_permit.created_at + (iexpiry * 1s) < now)
  end
}

entry set_default_expiry(v : nat) {
  called by owner
  require { r6: is_not_paused() }
  effect {
    default_expiry := v
  }
}

/**
* Sets expiry for permit
* @param v option of expiry value
* @param p option of permit
*/
entry set_expiry(v : option<nat>, p : option<bytes>) {
  require { r7: is_not_paused() }
  effect {
    match v with
      | some v_expiry ->
          do_fail_if(v_expiry >= default_expiry, EXPIRY_TOO_BIG)
      | none -> ()
    end;
    if permits.contains(caller)
    then begin
      match p with
      | some lpermit ->
        if permits[caller].user_permits.contains(lpermit)
        then begin
          var need_update = true;
          match v with
            | some v_expiry ->
              begin
                if v_expiry = 0 then
                begin
                  need_update := false;
                  permits[caller].user_permits.remove(lpermit)
                end
              end
            | none -> ()
          end;
          if need_update then
          begin
            const up : user_permit ?= permits[caller].user_permits[lpermit];
            permits[caller].user_permits.update(lpermit, some({up with expiry = v}))
          end
        end
        else fail ((PERMIT_NOT_FOUND, (caller, lpermit)))
      | none -> permits.update(caller, { user_expiry = v })
      end
    end
    else fail ((PERMIT_USER_NOT_FOUND, caller))
  }
}

//----------------------------------------------------------------------------
// Feeless Permit (TZIP17)
//----------------------------------------------------------------------------

/**
* Adds a permit
* @param pk public key that signed
* @param signature contract address * chain id * counter * data
* @param data serialisation and hash (blake2B * pack) of transfer parameters (list of pairs of 'from' address and transfer destination)
* Note: counter is to prevent replay attack
*/
entry permit(pk : key, sig : signature, data : bytes) {
  require { r5: is_not_paused() }
  effect {
    const user : address = key_to_address(pk);
    const empty : map<bytes, user_permit> = [];
    const pcounter_user_permits : (nat * map<bytes, user_permit>) =
      if permits.contains(user) then
        (permits[user].counter, permits[user].user_permits)
      else (0, empty);
    const pcounter = pcounter_user_permits[0];
    const puser_permits = pcounter_user_permits[1];
    const tosign : bytes = pack(((self_address, self_chain_id), (pcounter, data)));
    do_require(check_signature(pk, sig, tosign), (MISSIGNED, tosign));
    const lexpiry = get_default_expiry(user);
    permits.add_update(user, { counter += 1; user_permits = put(puser_permits, data, { expiry = some(lexpiry); created_at = now})});
    for (k, v) in permits[user].user_permits do
      if has_expired(v, lexpiry)
      then permits[user].user_permits.remove(k)
    done
  }
}

//----------------------------------------------------------------------------
// Feeless transfer (one step)
//----------------------------------------------------------------------------

/**
* Transfers tokens with permits
* @param batch list of pairs of permits (key and signature) and transfer specification
* Note: a transfer specification is a list of pairs of 'from' address and transfer destination
* Note: a transfer destination has a 'to' address, a token id and an amount
*/
entry transfer_gasless (batch : list<list<address * list<transfer_destination>> * (key * signature)>) {
  for b in batch do
    const pk  = b[1][0];
    const sig = b[1][1];
    const pkh = key_to_address(pk);

    const lcounter = if permits.contains(pkh) then permits[pkh].counter else 0;
    const data : bytes = pack((self_address, lcounter, blake2b(pack(b[0]))));
    if not check_signature(pk, sig, data) then fail((MISSIGNED, data));
    permits.add_update(pkh, { counter = (lcounter + 1)});

    for tx in b[0] do
      const %from = tx[0];
      const tds = tx[1];
      do_require(pkh = %from, (SIGNER_NOT_FROM, (pkh, %from)));
      for td in tds do begin
        const %to = td.to_dest;
        (* set token ownership *)
        const tokenid = td.token_id_dest;
        if ledger.contains(tokenid) then
          do_require(ledger[tokenid].lowner = %from, FA2_INSUFFICIENT_BALANCE)
        else fail(TOKEN_NOT_FOUND);
        if td.token_amount_dest <> 1 then
          fail(FA2_INVALID_AMOUNT)
        else
          ledger.update(tokenid, { lowner = %to });
      end done;
    done
  done
}

//----------------------------------------------------------------------------
// FA2 transfer (with permit check)
//----------------------------------------------------------------------------

function get_from_address(txs : list<address * list<transfer_destination>>) : option<address> {
  match txs with
  | hd::tl -> begin
    const %from = hd[0];
    for tx in tl do
      do_require(%from = tx[0], FA2_NOT_OPERATOR)
    done;
    return some(%from)
  end
  | [] -> return none
  end
}

/**
* Transfers tokens
* @param txs list of pairs of 'from' address and transfer destination
* Note : a transfer destination has a 'to' address, a token id and an amount
* Note : if caller is not 'from' nor operator, it checks if there is a permit
*/
entry %transfer (txs : list<address * list<transfer_destination>>) {
  require { r8 : is_not_paused() }
  effect {
    if not check_owner_and_operator(txs) then begin
      match get_from_address(txs) with
      | some(%from) -> begin
          const permit_key = blake2b(pack(txs));
          if not permits.contains(%from)
          then fail(NO_ENTRY_FOR_USER)
          else if not permits[%from].user_permits.contains(permit_key)
          then fail(FA2_NOT_OPERATOR)
          else (
            const iuser_permit ?= permits[%from].user_permits[permit_key];
            const iexpiry = get_expiry(%from, permit_key);
            if has_expired(iuser_permit, iexpiry)
            then fail(PERMIT_EXPIRED)
            else permits[%from].user_permits.remove(permit_key)
          );
          end
      | none -> ()
      end
    end;

    for tx in txs do
      const %from = tx[0];
      const tds = tx[1];
      for td in tds do begin
        do_require(td.token_amount_dest = 1, FA2_INVALID_AMOUNT);
        const tokenid = td.token_id_dest;
        if ledger.contains(tokenid) then
          do_require(ledger[tokenid].lowner = %from, FA2_INSUFFICIENT_BALANCE)
        else fail(TOKEN_NOT_FOUND);
        ledger.update(tokenid, { lowner = td.to_dest });
      end done;
    done
  }
}

//----------------------------------------------------------------------------
// Getters & views
//----------------------------------------------------------------------------

/**
* TZIP-4 view (aka Archetype getter) to return the list of balances for each balance request
* @param requests list of balance requests
*/
getter balance_of (requests : list<balance_of_request>) : list<balance_of_response> {
  return map(requests, br -> {
    request = br;
    balance_ = (
      if ledger.contains(br.btoken_id) and ledger[br.btoken_id].lowner = br.bo_owner
      then 1
      else 0
    )
  })
}

/**
* View (since Hangzhou) to return token's royalties
* @param tokenid token id
* Note: returns empty list if token does not exist in royalties map
*/
view get_royalties(tokenId : nat) : list<part> {
  return (royalties[tokenId]?.rvalue ? the : [])
}

//----------------------------------------------------------------------------
// Mint & Brun
//----------------------------------------------------------------------------

/**
* Mint new token
* @param itokenid token id to mint
* @param iowner owner of the token
* @param itokenMetadata token metadata
* @param iroyalties token's royalties
*/
entry mint (itokenid : nat, iowner : address, itokenMetadata: map<string, bytes>, iroyalties : list<part>) {
  called by owner or minters
  require { r11: is_not_paused() }
  effect {
    ledger.add({ ltoken = itokenid; lowner = iowner});
    token_metadata.add({ftoken_metadata = itokenid; token_id = itokenid; token_info = itokenMetadata});
    royalties.add({ itokenid; iroyalties });
  }
}

/**
* Burn an amount of token
* @param itokenid token to burn
*/
entry burn(itokenid : nat) {
  require { r2 : is_not_paused() }
  effect {
    if ledger.contains(itokenid) and ledger[itokenid].lowner = caller then begin
      ledger.remove(itokenid);
      token_metadata.remove(itokenid);
      royalties.remove(itokenid)
    end else
      fail(CALLER_NOT_OWNER);
  }
}
